---
title: "knitr engine test page"
output: html_document
---

```{r}
maxima.options(engine.format = "latex", 
	       inline.format = "inline", 
	       inline.label = FALSE)
```

```{maxima}
L: sqrt(1 - 1/R^2);
assume(R > 0);
'integrate(x, x, 0, L) = integrate(x, x, 0, L);
```

```{maxima}
'L = L;
'integrate(x, x, 0, 'L) = integrate(x, x, 0, L);
```

This is an inline test: `r maxima.inline("'L = L;")`.


```{maxima}
sqrt(3/4);
```

```{maxima}
f(x) := e^(x^2)$
diff(f(x), x);
```

```{maxima}
%;
```

```{maxima}
log(%o1);
```

```{maxima}
area(dist) := integrate(dist, x, minf, inf)$
mean(dist) := area(dist*x)$
EX2(dist) := area(dist*x^2)$
variance(dist) := EX2(dist) - mean(dist)^2$
mgf(dist) := area(dist*%e^(x*t))$
```

# Plots

```{maxima, fig.cap = "plot2d()", fig.align="center", fig.show='hide'}
r: (exp(cos(t))-2*cos(4*t)-sin(t/12)^5)$
plot2d([parametric, r*sin(t), r*cos(t), [t,-8*%pi,8*%pi]]);
```

```{maxima, fig.cap = "plot3d()", fig.align="center", fig.show='hide'}
plot3d(log (x^2*y^2), [x, -2, 2], [y, -2, 2],[grid, 29, 29],
       [palette, [gradient, red, orange, yellow, green]],
       color_bar, [xtics, 1], [ytics, 1], [ztics, 4],
       [color_bar_tics, 4]);
```

```{r, warning=FALSE}
pft <- list.files(pattern = "plot(2|3)d-[[:print:]]+\\.(png|pdf)", full.names = TRUE)
digest(readLines(pft[1]), algo = "sha256")
digest(readLines(pft[2]), algo = "sha256")
```


# Normal Distribution

```{maxima}
normal(x) := 
      (2*%pi*sigma^2)^(-1/2) * 
      exp(-(x-mu)^2/(2*sigma^2));

assume(sigma > 0)$

area(normal(x));
mean(normal(x));
variance(normal(x));
mgf(normal(x));
```

# Laplace Distribution

```{maxima}
laplace(x) := (2*b)^-1 * exp(-abs(x - mu)/b);

load("abs_integrate")$

assume(b > 0)$

area(laplace(x));
mean(laplace(x));
variance(laplace(x));
```

# Exponential Distribution

```{maxima}
expo(x) := unit_step(x) * lambda * exp(-lambda * x);

assume(lambda > 0)$

area(expo(x));
mean(expo(x));
variance(expo(x));
```

# Matrices

```{maxima}
m: matrix([0, 1, a], [1, 0, 1], [1, 1, 0]);
transpose(m);
determinant(m);
f: invert(m), detout;
m . f;
expand(%);
factor(%);
```

# If-then-else

```{maxima}
x: 1234;
y: 2345;
```

```{maxima}
if x > y
  then x
  else y;
```
